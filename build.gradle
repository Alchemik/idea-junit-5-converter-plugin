import org.jsoup.Jsoup
import org.jsoup.nodes.Document

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.16'
    id "org.asciidoctor.jvm.convert" version "3.1.0"
}

group 'de.wlsc'
version '1.1.3'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.javaparser:javaparser-core:3.15.11'
    implementation 'org.apache.commons:commons-lang3:3.9'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0'
    testRuntime 'org.junit.vintage:junit-vintage-engine:5.6.0'
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = "6.1.1"
    distributionType = Wrapper.DistributionType.ALL
}

asciidoctor {
    sourceDir file('.')
    outputDir file('build/docs')
    sources {
        include 'README.adoc'
    }
}

intellij {
    plugins = ['java']
}

patchPluginXml {
    dependsOn asciidoctor
    sinceBuild '181'
    untilBuild = ''
    changeNotes = {
        getReadme().select(".sect2").html()
    }
    pluginDescription = {
        getReadme().select(".sect1").get(0).select(".sectionbody").html()
    }
}

Document getReadme() {
    Jsoup.parse(file('build/docs/README.html').getText('UTF-8'))
}
