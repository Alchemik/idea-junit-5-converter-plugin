plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.15'
    id "org.asciidoctor.jvm.convert" version "3.0.0-alpha.4"
}

import org.jsoup.Jsoup
import org.jsoup.nodes.Document

group 'de.wlsc'
version '1.0.0'

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.javaparser:javaparser-core:3.15.7'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.2'
    testRuntime 'org.junit.vintage:junit-vintage-engine:5.5.2'
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = "6.0.1"
    distributionType = Wrapper.DistributionType.ALL
}

asciidoctor {
    sourceDir file('.')
    outputDir file('build/docs')
    sources {
        include 'README.adoc'
    }
}

intellij {
    plugins = ['java']
}

patchPluginXml {
    dependsOn asciidoctor
    sinceBuild '181'
    changeNotes = {
        getReadme().select(".sect2").html()
    }
    pluginDescription = {
        getReadme().select(".sect1").get(0).select(".sectionbody").html()
    }
}

Document getReadme() {
    Jsoup.parse(file('build/docs/README.html').getText('UTF-8'))
}
